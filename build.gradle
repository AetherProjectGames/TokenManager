import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

group 'me.realized'
version '3.2.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

project.tasks.assemble.dependsOn project.tasks.shadowJar
jar.enabled = false

compileJava.options.encoding = 'UTF-8'

targetCompatibility = 1.8

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources {
    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded"

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [NAME: project.name, VERSION: project.version, SHADED_GROUP: group])
    }
}

artifacts {
    archives jar
}

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(ShadowJar) {
    destinationDir = file("$rootDir/out/")
}

tasks.withType(Jar) {
    destinationDir = file("$rootDir/out/")
}

repositories {
    mavenCentral()

    maven {
        name 'spigot-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
    }

    maven {
        name 'bungeecord-repo'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        name 'vault-repo'
        url 'http://nexus.hc.to/content/repositories/pub_releases'
    }

    maven {
        name 'mvdw-repo'
        url 'http://repo.mvdw-software.be/content/groups/public/'
    }

    maven {
        name 'clip-repo'
        url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven {
        name 'bstats-repo'
        url 'http://repo.bstats.org/content/repositories/releases/'
    }

    maven {
        name 'inventive-repo'
        url 'https://repo.inventivetalent.org/content/groups/public/'
    }
}

dependencies {
    compile 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    compile 'net.milkbowl.vault:VaultAPI:1.6'
    compile 'me.clip:placeholderapi:2.8.7'
    compile ('be.maximvdw:MVdWPlaceholderAPI:2.5.1-SNAPSHOT') { transitive = false }
    compile 'org.projectlombok:lombok:1.16.20'

    compile 'redis.clients:jedis:2.9.0'
    compile 'org.apache.commons:commons-pool2:2.4.2'
    compile 'com.zaxxer:HikariCP:2.7.8'
    compile 'org.slf4j:slf4j-nop:1.7.25'
    compile 'org.bstats:bstats-bukkit:1.2'
    compile 'org.inventivetalent.spiget-update:bukkit:1.4.0'
}

shadowJar {
    baseName = 'TokenManager'
    classifier = null

    dependencies {
        include(dependency('redis.clients:.*'))
        include(dependency('org.apache.commons:.*'))
        include(dependency('com.zaxxer:.*'))
        include(dependency('org.slf4j:.*'))
        include(dependency('org.bstats:.*'))
        include(dependency('org.inventivetalent.spiget-update:.*'))
    }

    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded."

    relocate 'redis.clients', group + 'redis.clients'
    relocate 'org.apache.commons.pool2', group + 'apache.commons.pool2'
    relocate 'com.zaxxer.hikari', group + 'zaxxer.hikari'
    relocate 'org.slf4j', group + "slf4j"
    relocate 'org.bstats', group + 'bstats'
    relocate 'org.inventivetalent.update.spiget', group + 'update.spiget'
}